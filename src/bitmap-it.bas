#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
1 POKE55,0:POKE56,64:CLR:PRINT"BITMAP IT! V1.0":POKE53269,0:GOSUB138
2 PRINT"INITIALIZING..."
3 V=53248:TD=1024:CN=55296:HD=24576:XP=0:YP=0:CD=16384:PB=CD+1016:SB=PB+8
4 BG=V+33:BO=V+32:NY=18432:NN=17920
5 FL=49152:CP=49184:OP=53247:L1=251:H1=252:L2=253:H2=254
6 XP=24:YP=50:BY=HD:BI=6:RW=0:CO=0:X1=V:Y1=V+1:BL=0
7 PRINT"DONE.":SYS49200
8 GOTO166
9 PRINT"ERASE ALL CURRENT IMAGE DATA?":PRINT"ONCE THIS IS DONE IT";
10 PRINT" CANNOT BE UNDONE...PROCEED?"
11 GOSUB249:IFZ$<>"Y"THEN8
12 POKEL1,0:POKEH1,96:POKEL2,0:POKEH2,128:POKEOP,0:SYSFL
13 POKE56576,6:POKE648,64
14 POKE53272,(PEEK(53272)AND15)OR0
15 POKE53272,PEEK(53272)OR8
16 POKE53265,PEEK(53265)OR32
17 POKE53270,PEEK(53270)OR16
18 POKEX1,24:POKEY1,50:POKEV+21,1
19 C1=1:C2=13:C3=7:CC=C1:C0=0:PN=1:XP=24:YP=50
20 POKEBO,CC:POKEBG,C0:GOSUB256
21 GETZ$:K=PEEK(197)
22 IFZ$=""THENCX=0:CY=-1:GOTO47
23 IFZ$=""THENCX=2:CY=0 :GOTO47
24 IFZ$=""THENCX=0:CY=1 :GOTO47
25 IFZ$=""THENCX=-2:CY=0:GOTO47
26 IFZ$="W"THENCX=0:CY=-8:GOTO47
27 IFZ$="D"THENCX=8:CY=0 :GOTO47
28 IFZ$="S"THENCX=0:CY=8 :GOTO47
29 IFZ$="A"THENCX=-8:CY=0:GOTO47
30 IFZ$=" "ANDPN=ABS(PN)THEN55
31 IFZ$=CHR$(13)THENPN=-PN:IFPN<0THENGOSUB57
32 IFK>2ANDK<7THEN38
33 IFZ$="E"THENFB=0:GOTO88
34 IFZ$="F"THEN84
35 IFZ$="C"THEN113
36 IFZ$="X"THEN125
37 GOTO21
38 IFZ$=""THENCC=C1:GOTO82
39 IFZ$=""THENCC=C2:GOTO82
40 IFZ$=""THENCC=C3:GOTO82
41 IFZ$=""THENCC=C0:GOTO82
42 IFZ$=""THENCC=C1:GOTO69
43 IFZ$=""THENCC=C2:GOTO69
44 IFZ$=""THENCC=C3:GOTO69
45 IFZ$=""THENCC=C0:GOTO69
46 GOTO21
47 IFXP+CX>342ORXP+CX<24ORYP+CY>249ORYP+CY<50THEN21
48 OB=BL:XP=XP+CX:YP=YP+CY:RW=YP-50:CO=XP-24:BY=(INT(RW/8)*320)+(INT(CO/8)*8)
49 BY=BY+(RW-INT(RW/8)*8):BI=6-(CO-INT(CO/8)*8):BL=INT(BY/8):BY=BY+HD
50 IFXP>255THENPOKEV+16,1:POKEX1,XP-256:GOTO52
51 POKEX1,XP:POKEV+16,0
52 POKEY1,YP
53 IFPN<>ABS(PN)THENGOSUB57
54 GOTO21
55 GOSUB57
56 GOTO21
57 IFCC=C1THENGOSUB62
58 IFCC=C2THENGOSUB64
59 IFCC=C3THENGOSUB66
60 IFCC=C0THENGOSUB68
61 RETURN
62 IFBL<>OBTHENPOKECD+BL,(PEEK(CD+BL)AND15)ORC1*16
63 POKEBY,PEEK(BY)AND(255-2(BI+1)):POKEBY,PEEK(BY)OR(2BI):RETURN
64 IFBL<>OBTHENPOKECD+BL,(PEEK(CD+BL)AND240)ORC2
65 POKEBY,PEEK(BY)OR(2(BI+1)):POKEBY,PEEK(BY)AND(255-2BI):RETURN
66 IFBL<>OBTHENPOKECN+BL,C3
67 POKEBY,PEEK(BY)OR(2(BI+1)):POKEBY,PEEK(BY)OR(2BI):RETURN
68 POKEBY,PEEK(BY)AND(255-2(BI+1)):POKEBY,PEEK(BY)AND(255-2BI):RETURN
69 NC=CC
70 GOSUB249
71 IFZ$=""THENNC=NC+1:GOTO75
72 IFZ$=""THENNC=NC-1:GOTO75
73 IFZ$=CHR$(13)THEN78
74 GOTO70
75 IFNC<0THENNC=15
76 IFNC>15THENNC=0
77 POKEBO,NC:GOTO70
78 IFCC=C1THENC1=NC:CC=C1:POKECD+BL,(PEEK(CD+BL)AND15)OR(C1*16):GOTO21
79 IFCC=C2THENC2=NC:CC=C2:POKECD+BL,(PEEK(CD+BL)AND240)ORC2:GOTO21
80 IFCC=C3THENC3=NC:CC=C3:POKECN+BL,C3:GOTO21
81 IFCC=C0THENC0=NC:CC=C0:POKEBG,C0:GOTO21
82 POKEBO,CC
83 GOTO21
84 IFCC=C1THENFB=85
85 IFCC=C2THENFB=170
86 IFCC=C3THENFB=255
87 IFCC=C0THENFB=0
88 XX=INT((XP-24)/8)*8+24:YY=INT((YP-50)/8)*8+50:POKEV+21,0
89 POKEOP,FB:SN=1:GOSUB97:P1=PP:SN=2:GOSUB97:P2=PP
90 IFP1=P2THENPOKEV+21,1:GOTO21
91 IFP2<P1THENP2=P1:P1=PP
92 POKEL1,P1-INT(P1/256)*256:POKEH1,P1/256:POKEL2,P2-INT(P2/256)*256
93 POKEH2,P2/256:SYSFL
94 J=(P1-HD)/8:K=(P2-HD)/8
95 FORI=JTOK:POKECD+I,C1*16+C2:POKECN+I,C3:NEXT
96 POKEV+21,1:GOTO21
97 POKEV+21,PEEK(V+21)OR2SN:SX=V+SN*2:SY=V+SN*2+1:POKESX,XX:POKESY,YY
98 GETZ$
99 IFZ$=""THENYY=YY-8:GOTO105
100 IFZ$=""THENXX=XX+8:GOTO105
101 IFZ$=""THENYY=YY+8:GOTO105
102 IFZ$=""THENXX=XX-8:GOTO105
103 IFZ$=CHR$(13)THEN111
104 GOTO98
105 IFYY<50THENYY=YY+8
106 IFYY>249THENYY=YY-8
107 IFXX<24THENXX=XX+8
108 IFXX>342THENXX=XX-8
109 IFXX>255THENPOKEV+16,PEEK(V+16)OR2SN:POKESX,XX-256:POKESY,YY:GOTO98
110 POKEV+16,PEEK(V+16)AND255-2SN:POKESX,XX:POKESY,YY:GOTO98
111 RW=YY-50:CO=XX-24:PP=HD+(INT(RW/8)*320)+(CO)+(RW-(INT(RW/8)*8))
112 RETURN
113 XX=INT((XP-24)/8)*8+24:YY=INT((YP-50)/8)*8+50
114 POKEV+21,0:SN=1
115 GOSUB97:P1=PP:SN=2:GOSUB97:P2=PP
116 IFP2=P1THENPOKEV+21,1:GOTO21
117 IFP2<P1THENP2=P1:P1=PP
118 IFP2-P1>255THENPOKEV+21,1:GOTO21
119 SN=3:GOSUB97:P3=PP
120 POKEOP,P2-P1:POKEL1,P1-INT(P1/256)*256:POKEH1,P1/256:POKEH2,P3/256
121 POKEL2,P3-INT(P3/256)*256:SYSCP
122 J=(P1-P2)/8:K=(P1-HD)/8:Q=(P3-HD)/8
123 FORI=0TOJ:POKECD+Q,PEEK(CD+K):POKECN+Q,PEEK(CN+K):NEXT
124 POKEV+21,1:GOTO21
125 POKE53270,PEEK(53270)AND239
126 POKE53265,PEEK(53265)AND223
127 POKE53272,PEEK(53272)AND247
128 POKE53272,(PEEK(53272)AND15)OR16
129 POKE648,4:POKE56576,7:POKEV+21,0
130 GOSUB251
131 GOTO8
132 PRINT"BITMAP IT! V1.0(C) 05/25/20"
133 PRINT"DESIGNED & DEVELOPED BY";
134 PRINT" FRANCIS DESANY"
135 PRINT"PIXEL ENTERTAINMENT(R) 2020"
136 PRINT:PRINT:SYS49248:END
137 END
138 PRINT"WELCOME TO BITMAP IT!, IN ORDER TO  ";
139 PRINT"PROCEED WITH THE PROGRAM, PLEASE ENSURE ";
140 PRINT"THAT THE RAM OF YOUR COMPUTER HAS BEEN  ";
141 PRINT"PROPERLY INITIALIZED BY THIS SETUP      ";
142 PRINT"PROGRAM, IF NOT, PRESS THE  SPACE BAR  ";
143 PRINT"TO CONTINUE..."
144 PRINT"OTHERWISE, PRESS  ANY OTHER KEY  TO ";
145 PRINT"RUN THE USEFUL PART OF THIS SOFTWARE    ";
146 PRINT"THANK YOU!"
147 GOSUB249:IFZ$<>" "THENRETURN
148 PRINT"LOADING FILL AND COPY ROUTINES..."
149 FORI=49152TO49177:READA:POKEI,A:NEXT:FORI=49184TO49196:READA:POKEI,A:NEXT
150 FORI=0TO24:READA:POKE49200+I,A:NEXT:FORI=0TO14:READA:POKE49232+I,A:NEXT
151 FORI=0TO14:READA:POKE49248+I,A:NEXT
152 DATA 160,0,173,255,207,145,251,230,251,208,2,230,252,166,251,228,253,208
153 DATA 242,166,252,228,254,208,236,96
154 DATA 160,0,177,251,145,253,200,204,255,207,208,246,96
155 DATA 173,20,3,141,254,192,173,21,3,141,255,192,120,169,80,141,20,3,169,192
156 DATA 141,21,3,88,96
157 DATA 238,39,208,238,40,208,238,41,208,238,42,208,108,254,192
158 DATA 120,173,254,192,141,20,3,173,255,192,141,21,3,88,96
159 POKE17400,16:POKE17401,16:POKE17402,16:POKE17403,16
160 POKE17408,192:FORI=0TO62
161 POKE17409+I,0:NEXT:V=53248
162 POKEV+16,0:POKEV+21,0:POKEV+23,0:POKEV+28,0:POKEV+29,0
163 PRINT"DONE."
164 RETURN
165 END
166 PRINT"";:POKEV+32,0:POKEV+33,0
167 PRINT"                                        ";
168 PRINT"               BITMAP IT!               ";
169 PRINT"                                        ";
170 PRINT"MENU FUNCTIONS...                       ";
171 PRINT"N' = NEW IMAGE"
172 PRINT"E' = EDIT IMAGE"
173 PRINT"L' = LOAD IMAGE FROM DISKETTE"
174 PRINT"S' = SAVE IMAGE TO DISKETTE"
175 PRINT"Q' = QUIT BITMAP IT!"
176 PRINT"EDITOR FUNCTIONS...                     ";
177 PRINT"FN = USE/SELECT LOCAL BLOCK COLOR #N";
178 PRINT"RT = TOGGLE PEN UP/DOWN"
179 PRINT"SP = DRAW PIXEL (PEN UP ONLY)"
180 PRINT"E' = ERASE CUSTOM SECTION OF IMAGE"
181 PRINT"F' = FILL CUSTOM SECTION OF IMAGE"
182 PRINT"C' = COPY & PASTE CUSTOM SECTION"
183 PRINT"CR = MOVE PEN (HD)"
184 PRINT"WD = PEN JUMP"
185 PRINT"X' = EXIT TO MENU SCREEN"
186 PRINT" * PIXEL(R) BITMAP IT! V1.0, 05/25/20*  ";
187 GOSUB249
188 IFZ$="N"THEN9
189 IFZ$="E"THEN13
190 IFZ$="L"THENGOSUB195:GOTO166
191 IFZ$="S"THENGOSUB221:GOTO166
192 IFZ$="Q"THEN132
193 GOTO187
194 END
195 PRINT"ARE YOU SURE YOU WISH TO LOAD A HI-RES"
196 PRINT"IMAGE FROM DISK? THIS WILL REPLACE ALL"
197 PRINT"CURRENT IMAGE & COLOR DATA IN THE"
198 PRINT"COMPUTER'S RAM. CONTINUE?"
199 GOSUB249:IFZ$<>"Y"THENRETURN
200 PRINT"STARTING TRACK & SECTOR NUMBER";:INPUT TT,SS
201 PRINT"LOADING BITMAP DATA..."
202 OPEN15,8,15:OPEN2,8,2,"#"
203 P1=HD:P2=HD+8191:GOSUB216
204 IFAF=1THENCLOSE2:CLOSE15:RETURN
205 PRINT"LOADING COLORS #1 & #2..."
206 P1=CD:P2=CD+1023:GOSUB216
207 IFAF=1THENCLOSE2:CLOSE15:RETURN
208 PRINT"LOADING COLOR NYBBLE (#3)...":P1=NN:P2=NN+511:GOSUB216
209 IFAF=1THENCLOSE2:CLOSE15:RETURN
210 PRINT"LOAD COMPLETE."
211 CLOSE2:CLOSE15
212 PRINT"DECOMPRESSING COLOR NYBBLE...":FORI=0TO999STEP2
213 POKENY+I,(PEEK(NN+I/2)AND15):POKENY+I+1,((PEEK(NN+I/2)AND240)/16):NEXTI
214 PRINT"DONE.":FORT=0TO2000:NEXT
215 RETURN
216 FORI=P1TOP2STEP256:PRINT#15,"U1 2 0";TT;SS:FORJ=0TO255:GET#2,A$
217 POKEI+J,ASC(A$+CHR$(0)):NEXTJ
218 GOSUB266:IFAF=1THENRETURN
219 PRINT#15,"B-P 2 0":NEXTI
220 RETURN
221 PRINT"ARE YOU SURE YOU WISH TO SAVE?"
222 GOSUB249:IFZ$<>"Y"THENRETURN
223 PRINT"STARTING TRACK,SECTOR ON DISKETTE";:INPUT TT,SS:GOSUB261
224 PRINT"THIS OPERATION WILL REPLACE ALL DATA"
225 PRINT"LOCATED ON TRACK #"TT", SECTOR #"SS
226 PRINT"AND THE FOLLOWING 37 SECTORS ON"
227 PRINT""DN$" --- CONTINUE?"
228 GOSUB249:IFZ$<>"Y"THENRETURN
229 PRINT"COMPRESSING COLOR NYBBLE...":FORI=0TO999STEP2
230 POKENN+I/2,(PEEK(NY+I)AND15)+((PEEK(NY+I+1)AND15)*16):NEXT:PRINT"DONE."
231 PRINT"SAVING BITMAPPED DATA..."
232 OPEN15,8,15:OPEN2,8,2,"#"
233 P1=HD:P2=HD+8191:GOSUB244
234 IFAF=1THENCLOSE2:CLOSE15:RETURN
235 PRINT"SAVING COLORS #1 & #2..."
236 P1=CD:P2=CD+1023:GOSUB244
237 IFAF=1THENCLOSE2:CLOSE15:RETURN
238 PRINT"SAVING COLOR NYBBLE (#3)..."
239 P1=NN:P2=NN+511:GOSUB244
240 IFAF=1THENCLOSE2:CLOSE15:RETURN
241 PRINT"SAVE COMPLETE."
242 CLOSE2:CLOSE15:FORT=0TO2000:NEXT
243 RETURN
244 FORI=P1TOP2STEP256:FORJ=0TO255:PRINT#2,CHR$(PEEK(I+J));:NEXTJ
245 PRINT#15,"U2 2 0";TT;SS:GOSUB266
246 IFAF=1THENRETURN
247 PRINT#15,"B-P 2 0":NEXTI
248 RETURN
249 GETZ$:IFZ$<>""THENRETURN
250 GOTO249
251 POKEOP,0:FORI=0TO3
252 P2=NY+I*256:P1=CN+I*256
253 POKEL1,P1-INT(P1/256)*256:POKEH1,P1/256:POKEL2,P2-INT(P2/256)*256
254 POKEH2,P2/256:SYS49184:NEXT
255 RETURN
256 POKEOP,0:FORI=0TO3
257 P2=NY+I*256:P1=CN+I*256
258 POKEL2,P1-INT(P1/256)*256:POKEH2,P1/256:POKEL1,P2-INT(P2/256)*256
259 POKEH1,P2/256:SYS49184:NEXT
260 RETURN
261 DN$="":OPEN 15,8,15:OPEN 2,8,2,"#"
262 PRINT#15,"U1 2 0 18 0"
263 PRINT#15,"B-P 2 144"
264 FORI=0TO15:GET#2,A$:DN$=DN$+A$:NEXT:CLOSE2:CLOSE15
265 RETURN
266 SS=SS+1
267 IFTT>30THEN272
268 IFTT>24THEN274
269 IFTT>17THEN276
270 IFSS>20THENTT=TT+1:SS=0:IFTT=18THENSS=2
271 RETURN
272 IFSS>16THENTT=TT+1:SS=0:IFTT>35THENTT=1:SS=0:GOSUB278
273 RETURN
274 IFSS>17THENTT=TT+1:SS=0
275 RETURN
276 IFSS>18THENTT=TT+1:SS=0
277 RETURN
278 PRINT"END OF DISK ERROR. FLIP OR REPLACE"
279 PRINT"DISKETTE, DATA WILL RESUME SAVING FROM"
280 PRINT"TRACK 1, SECTOR 0 OF NEW DISKETTE..."
281 PRINT"SPACE = RESUME : X = ABORT SAVE"
282 GOSUB249
283 IFZ$=" "THENAF=0:CLOSE2:CLOSE15:OPEN15,8,15:OPEN2,8,2,"#":PRINT"OK":RETURN
284 IFZ$="X"THENAF=1:RETURN
285 GOTO282
